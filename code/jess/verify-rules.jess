(defrule causes-feature-verify
	"If we have observed a contradicting observable, a state is impossible"
	(observable
		{observed == FALSE}
		(id ?oid)
	)
 	(causes-feature
		{observable == ?oid}
	 	(component ?cid)
		(component-state ?cstate)
	)
	?c <- (component
	 	{id == ?cid}
		(impossible-states $?ips&:(not (member$ ?cstate ?ips)))
	)
	=>
	(modify ?c
		(impossible-states (create$ ?ips ?cstate))
	)
)

(defrule causes-from-wire-verify
	"If we know a contradicting conclusion of a state, that state is impossible"
	(component
		(id ?cid)
		(type ?ctype)
		(impossible-states $?cips)
	)
	(causes-from-wire
		{component-type == ?ctype}
		(component-state ?cistate&:(member$ ?cistate ?cips))
		(faulty-wire-state ?wstate)
	)
	?w <- (connection
		{out == ?cid}
		;(possible-states $?wps&:(member$ ?wstate ?wps))
		(impossible-states $?wips&:(not (member$ ?wstate ?wips)))
	)
	=>
	(modify ?w
		;(possible-states (complement$ ?wps (create$ ?wstate)))
		(impossible-states (create$ ?wips ?wstate))
	)
)

(defrule causes-to-wire-verify
	"If we know a contradicting conclusion of a state, that state is impossible"
	(connection
		(in ?cid)
		(impossible-states $?wips)
	)
	(component
		{id == ?cid}
		(type ?ctype)
	)
 	(causes-to-wire
	 	(faulty-comp-type ?ctype)
		(faulty-comp-state ?cstate)
		(wire-state ?wistate&:(member$ ?wistate ?wips))
	)
	?c <- (component
		{id == ?cid}
		(impossible-states $?cips&:(not (member$ ?cstate ?cips)))
	)
	=>
	(modify ?c
		(impossible-states (create$ ?cips ?cstate))
	)
)

(defrule possible-to-impossible
	"If a state is is impossible it is no longer possible"
	(component
		(id ?cid)
		(possible-states $?first ?cstate $?last)
		(impossible-states $?cips&:(member$ ?cstate ?cips))
	)
	?c <- (component
		{id == ?cid}
	)
	=>
	(modify ?c
		(possible-states (create$ ?first ?last))
	)
)
